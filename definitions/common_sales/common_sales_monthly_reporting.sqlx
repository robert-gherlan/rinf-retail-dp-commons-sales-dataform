config {
  type: "table",
  schema: "common_sales",
  name: "common_sales_monthly_reporting",
  description: "The Common Sales table that contains the monthly sales aggregated data.",
  columns: {
    store_id: "The ID of the store where the transaction is associated with.",
    store_name: "The name of the store where the transaction is associated with.",
    country_code: "The two letter country code of the country where the transaction took place. Ex: ES for Spain country.",
    country_name: "The name of the country where the transaction took place.",
    first_day_of_the_month: "The first day of the month when the transaction took place.",
    calendar_year: "The calendar year when the transaction took place. Now, we only have the sales data between 2023 and 2025.",
    currency: "The currency code for the transaction.",
    transaction_type: "The transaction type, and can have one of the following supported values: 'Sale' and 'Return'.",
    payment_method: "The payment method, and can have one of the following supported values: 'Credit Card' and 'Cash'.",
    product_category: "The category of the product that was sold and can have one of the following three values: 'Feminine', 'Masculine' and 'Children'.",
    quantity: "The quantity of products that was sold.",
    sales_amount: "The sales amount in local currency.",
    sales_amount_usd: "The sales amount in USD currency.",
    production_cost_amount: "The production cost amount in local currency.",
    production_cost_amount_usd: "The production cost amount in USD currency.",
    profit_amount: "The profit amount in the local currency.",
    profit_amount_usd: "The profit amount in USD currency.",
    store_gps_coordinates: "The GPS coordinates of the store."
  },
  assertions: {
    nonNull: ["country_name", "store_name"],
    rowConditions: [
      "calendar_year BETWEEN 2023 AND 2025",
      "product_category IN ('Feminine', 'Masculine', 'Children')"
    ]
  },
  tags: [tags.common_sales]
}

SELECT
  s.store_id,
  s.store_name,
  s.country_code,
  s.country_name,
  DATE_TRUNC(transaction_date, MONTH) AS first_day_of_the_month,
  EXTRACT(YEAR FROM transaction_date) AS calendar_year,
  currency,
  transaction_type,
  payment_method,
  product_category,
  SUM(quantity) AS quantity,
  SUM(line_total) AS sales_amount,
  SUM(line_total * exchange_rate) AS sales_amount_usd,
  SUM(quantity * product_production_cost) AS production_cost_amount,
  SUM(quantity * product_production_cost * exchange_rate) AS production_cost_amount_usd,
  SUM(line_total) - SUM(quantity * product_production_cost) AS profit_amount,
  SUM(line_total * exchange_rate) - SUM(quantity * product_production_cost * exchange_rate) AS profit_amount_usd,
  ST_UNION_AGG(s.gps_coordinates) AS store_gps_coordinates
FROM ${ref("common_sales")} cs
LEFT JOIN ${ref("stores_dim")} s USING (store_id)
GROUP BY ALL
ORDER BY country_name, store_name, first_day_of_the_month
