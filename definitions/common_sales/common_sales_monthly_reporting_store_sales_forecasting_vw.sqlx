config {
  type: "view",
  schema: "common_sales",
  name: "common_sales_monthly_reporting_store_sales_forecasting_vw",
  description: "The Common Sales view that contains the monthly forecasted sales using the ARIMA_PLUS_XREG ML model.",
  tags: [tags.common_sales]
}

SELECT
  store_name,
  country_name,
  EXTRACT(DATE FROM forecast_timestamp) AS first_day_of_the_month,
  forecast_value AS sales_amount_usd
FROM ML.FORECAST (
    MODEL ${ref("common_sales_monthly_reporting_store_sales_forecasting_model")},
    -- 12 represents the number of months for which the sales are forecasted.
    -- The 0.95 as the confidence level, the value that specifies the percentage of the future values that fall in the prediction interval.
    STRUCT (12 AS horizon, 0.95 AS confidence_level),
    (
      SELECT
        * EXCEPT (sales_amount_usd)
      FROM ${ref("common_sales_monthly_reporting")}
    )
  )

post_operations {
  GRANT `roles/bigquery.dataViewer` ON TABLE ${self()} TO "serviceAccount:sa-bigquery@rinf-retail-dp-commons-sales.iam.gserviceaccount.com", "user:gherlanilierobert@gmail.com"
}
