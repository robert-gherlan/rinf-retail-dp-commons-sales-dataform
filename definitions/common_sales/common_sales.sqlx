config {
  type: "table",
  schema: "common_sales",
  name: "common_sales",
  description: "The Common Sales table that contains all the sales data related of the retail store.",
  columns: {
    transaction_date: "The data when the transaction happened.",
    transaction_date_time: "The date and time when the transaction happened.",
    invoice_id: "Transaction Invoice ID",
    line: "Transaction line"
  },
  assertions: {
    nonNull: ["invoice_id", "payment_method", "transaction_type"],
    rowConditions: [
      "payment_method IN ('Credit Card', 'Cash')",
      "transaction_type IN ('Return', 'Sale')",
      "currency IS NOT NULL AND currency IN ('CNY', 'EUR', 'USD', 'GBP')",
      "currency_symbol IS NOT NULL AND currency_symbol IN ('£', '¥', '€', '$')",
      "(currency = 'EUR' AND currency_symbol = '€') OR (currency = 'CNY' AND currency_symbol = '¥') OR (currency = 'USD' AND currency_symbol = '$') OR (currency = 'GBP' AND currency_symbol = '£')"
    ]
  },
  bigquery: {
    partitionBy: "transaction_date"
  },
  tags: [tags.common_sales]
}

SELECT
  -- Start transaction related data
  CAST(t.transaction_date_time AS DATE) AS transaction_date,
  t.transaction_date_time,
  t.invoice_id,
  t.line,
  t.customer_id,
  t.product_id,
  t.size,
  t.color,
  t.store_id,
  t.employee_id,
  t.currency,
  t.currency_symbol,
  t.sku,
  t.transaction_type,
  t.payment_method,
  t.unit_price,
  t.quantity,
  t.discount,
  t.line_total,
  t.invoice_total,
  -- Start exchange rate
  e.exchange_rate,
  -- Start product related data
  p.category AS product_category,
  p.sub_category AS product_sub_category,
  p.color AS product_color,
  p.sizes AS product_sizes,
  p.production_cost AS product_production_cost,
  -- Start customer related data
  c.name,
  c.email,
  c.phone,
  c.city_name,
  c.country_name AS customer_country_name,
  c.gender,
  c.birth_date,
  c.job_title,
  -- Start discount related date
  d.description AS discount_description,
  d.category AS discount_category,
  d.sub_category AS discount_sub_category,
  -- Start store related data
  s.country_name AS store_country_name,
  s.store_name,
  s.number_of_employees,
  s.zip_code,
  s.gps_coordinates
FROM ${ref("transactions")} t
LEFT JOIN ${ref("customers")} c USING (customer_id)
LEFT JOIN ${ref("stores_dim")} s USING (store_id)
LEFT JOIN ${ref("products")} p USING (product_id)
LEFT JOIN ${ref("discounts")} d ON d.category = p.category AND d.sub_category = p.sub_category AND t.transaction_date_time BETWEEN d.start_date AND d.end_date
LEFT JOIN ${ref("usd_exchange_rates_daily")} e ON e.date = CAST(t.transaction_date_time AS DATE) AND e.currency = t.currency

post_operations {
  GRANT `roles/bigquery.dataViewer` ON TABLE ${self()} TO "serviceAccount:sa-bigquery@rinf-retail-dp-commons-sales.iam.gserviceaccount.com", "user:gherlanilierobert@gmail.com"
}
